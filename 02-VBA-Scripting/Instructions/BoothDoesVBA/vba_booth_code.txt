Sub StockAnalysis()

    ' Declare Current as a worksheet object variable.
    Dim ws As Worksheet

    ' Set an initial variable for holding the Stock Ticker
    Dim stock_ticker As String
    Dim i As Double
    
    'Find the last non-blank cell in column A(1)
    Dim last_row As Long
    
    ' Set an initial variable for holding the total volume per stock
    Dim volume_total As Double
    
    ' keep track of yearly change stuff
    Dim final_close As Double
    Dim first_open As Double
    Dim yearly_change As Double
    Dim percent_change As Double
    
    ' second summary table stuff
    Dim max_increase_ticker As String
    Dim max_increase As Double
    Dim min_increase_ticker As String
    Dim min_increase As Double
    Dim max_volume_ticker As String
    Dim max_volume As Double
        
    ' Loop through all of the worksheets in the active workbook.
    For Each ws In Worksheets
        ' let me know what worksheet we are on
        MsgBox (ws.Name)
        
        ' write out our summary table columns
        ws.Range("I1").Value = "Ticker"
        ws.Range("J1").Value = "Yearly Change"
        ws.Range("K1").Value = "Percent Change"
        ws.Range("L1").Value = "Total Volume"
        
        ws.Range("P1").Value = "Ticker"
        ws.Range("Q1").Value = "Value"
        
        ws.Range("O2").Value = "Greatest % Increase"
        ws.Range("O3").Value = "Greatest % Decrease"
        ws.Range("O4").Value = "Greatest Total Volume"
        
        ' initialize variables
        last_row = ws.Cells(Rows.Count, 1).End(xlUp).Row
        volume_total = 0
        
        percent_change = 0
        yearly_change = 0
        final_close = 0
        first_open = ws.Cells(2, 3).Value
        
        ' second summary table stuff
        max_increase = 0
        min_increase = 0
        max_volume = 0
        
        Dim summary_table_row As Integer
        summary_table_row = 2
        
        ' Loop through all stock prices
        For i = 2 To last_row
        
            ' Check if we are still within the same stock ticker, if we are not...
            If ws.Cells(i + 1, 1).Value <> ws.Cells(i, 1).Value Then
            
                ' Message Box the unique Stock Ticker
                ' MsgBox (Cells(i, 1).Value)
                
                ' pull the final closing price
                final_close = ws.Cells(i, 6).Value
                
                ' DO WORK
                volume_total = volume_total + ws.Cells(i, 7).Value ' Add to the Volume Total
                yearly_change = final_close - first_open ' calculate yearly change
                
                ' avoid divide by zero
                If first_open = 0 Then
                    percent_change = 0
                    ' percent_change = -1
                    ' percent_change = 100 * (yearly_change / 0.00000001)
                Else
                    percent_change = 100 * (yearly_change / first_open)
                End If
                
                stock_ticker = ws.Cells(i, 1).Value
                
                ' append to summary table
                ws.Cells(summary_table_row, 9).Value = stock_ticker
                ws.Cells(summary_table_row, 12).Value = volume_total
                ws.Cells(summary_table_row, 10).Value = yearly_change
                ws.Cells(summary_table_row, 11).Value = percent_change
                
                ' color code yearly change
                If yearly_change > 0 Then
                    ws.Cells(summary_table_row, 10).Interior.ColorIndex = 4
                Else
                    ws.Cells(summary_table_row, 10).Interior.ColorIndex = 3
                End If
                
                ' now reset our summary table and volume
                summary_table_row = summary_table_row + 1
                volume_total = 0
                
                ' now get open price of NEXT group
                first_open = ws.Cells(i + 1, 3).Value
                
            ' If the cell immediately following a row is the same stock...
            Else
            
              ' Add to the Volume Total
              volume_total = volume_total + ws.Cells(i, 7).Value
            
            End If
        
        Next i
        
        ' looping through the summary table
        For j = 2 To ws.Cells(Rows.Count, 9).End(xlUp).Row
            If (ws.Cells(j, 11).Value > max_increase) Then
                max_increase = ws.Cells(j, 11).Value
                max_increase_ticker = ws.Cells(j, 9).Value
            End If
            
            If (ws.Cells(j, 11).Value < min_increase) Then
                min_increase = ws.Cells(j, 11).Value
                min_increase_ticker = ws.Cells(j, 9).Value
            End If
            
            If (ws.Cells(j, 12).Value > max_volume) Then
                max_volume = ws.Cells(j, 12).Value
                max_volume_ticker = ws.Cells(j, 9).Value
            End If
        
        Next j
        
        ' write to second summary table
        ws.Range("P2").Value = max_increase_ticker
        ws.Range("Q2").Value = max_increase
        ws.Range("P3").Value = min_increase_ticker
        ws.Range("Q3").Value = min_increase
        ws.Range("P4").Value = max_volume_ticker
        ws.Range("Q4").Value = max_volume
        
    Next ' next worksheet

End Sub

